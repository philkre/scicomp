{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "84d4637a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Preperare distributed\n",
    "using Distributed\n",
    "addprocs(Sys.CPU_THREADS - nprocs() - 1)\n",
    "print(\"Number of workers: \", nprocs(), \"\\nNumber of CPU threads: \", Sys.CPU_THREADS, \"\\n\")\n",
    "\n",
    "@everywhere using SharedArrays\n",
    "using LoopVectorization\n",
    "using BenchmarkTools\n",
    "\n",
    "@everywhere using Plots\n",
    "\n",
    "@everywhere begin\n",
    "    ENV[\"GKSwstype\"] = \"nul\"  # Headless mode - no display\n",
    "end\n",
    "\n",
    "using FFMPEG"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ec762f40",
   "metadata": {},
   "source": [
    "# Problem 1.1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb2906d2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Setup for benchmarking\n",
    "N_test = 100000\n",
    "psi_tst = rand(N_test)\n",
    "do_bench = false\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b08d355b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation(psi, c, L, N)\n",
    "    # Starts from second point to second last point\n",
    "    d2psi_dt2 = c^2 * (psi[1:end-2] - 2 * psi[2:end-1] + psi[3:end]) / (L / N)^2\n",
    "    return d2psi_dt2\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1a978a1d",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_inb(psi, c, L, N)\n",
    "    dx2_inv = (N / L)^2\n",
    "    c2 = c^2\n",
    "    d2psi_dt2 = similar(psi, length(psi) - 2)\n",
    "    @inbounds for i in 1:length(d2psi_dt2)\n",
    "        d2psi_dt2[i] = c2 * (psi[i] - 2 * psi[i+1] + psi[i+2]) * dx2_inv\n",
    "    end\n",
    "    return d2psi_dt2\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_inb(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "952da36b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_vec(psi, c, L, N)\n",
    "    dx2_inv = (N / L)^2\n",
    "    c2 = c^2\n",
    "    @views @. c2 * (psi[1:end-2] - 2 * psi[2:end-1] + psi[3:end]) * dx2_inv\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_vec(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f287abe5",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_dist(psi, c, L, N)\n",
    "    n = length(psi) - 2\n",
    "    d2psi_dt2 = SharedArray{Float64}(n)\n",
    "\n",
    "    @distributed for i in 1:n\n",
    "        d2psi_dt2[i] = c^2 * (psi[i] - 2 * psi[i+1] + psi[i+2]) / (L / N)^2\n",
    "    end\n",
    "\n",
    "    return Array(d2psi_dt2)\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_dist(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "086323e4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_simd(psi, c, L, N)\n",
    "    n = length(psi) - 2\n",
    "    d2psi_dt2 = Vector{Float64}(undef, n)\n",
    "    dx2_inv = (N / L)^2\n",
    "    c2 = c^2\n",
    "\n",
    "    @inbounds @simd for i in 1:n\n",
    "        d2psi_dt2[i] = c2 * (psi[i] - 2 * psi[i+1] + psi[i+2]) * dx2_inv\n",
    "    end\n",
    "    return d2psi_dt2\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_simd(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2b2f9864",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_avx(psi, c, L, N)\n",
    "    n = length(psi) - 2\n",
    "    d2psi_dt2 = Vector{Float64}(undef, n)\n",
    "    dx2_inv = (N / L)^2\n",
    "    c2 = c^2\n",
    "\n",
    "    @inbounds @avx for i in 1:n\n",
    "        d2psi_dt2[i] = c2 * (psi[i] - 2 * psi[i+1] + psi[i+2]) * dx2_inv\n",
    "    end\n",
    "    return d2psi_dt2\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_avx(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c5405fb9",
   "metadata": {},
   "outputs": [],
   "source": [
    "function wave_equation_dist_avx(psi, c, L, N)\n",
    "    n = length(psi) - 2\n",
    "    d2psi_dt2 = SharedArray{Float64}(n)\n",
    "    dx2_inv = (N / L)^2\n",
    "    c2 = c^2\n",
    "\n",
    "    # Slice the array into chunks for each worker\n",
    "    @sync for p in workers()\n",
    "        @async begin\n",
    "            @fetchfrom p begin\n",
    "                local start_idx = (p - 1) * div(n, nprocs()) + 1\n",
    "                local end_idx = min(p * div(n, nprocs()), n) + 1\n",
    "                @inbounds @avx for i in start_idx:end_idx\n",
    "                    d2psi_dt2[i] = c2 * (psi[i] - 2 * psi[i+1] + psi[i+2]) * dx2_inv\n",
    "                end\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return Array(d2psi_dt2)\n",
    "end\n",
    "if do_bench\n",
    "    @benchmark wave_equation_dist_avx(psi_tst, 1, 1, N_test)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c610f28a",
   "metadata": {},
   "outputs": [],
   "source": [
    "initial_condition_0 = (x) -> sin( x)\n",
    "initial_condition_1 = (x) -> sin(2pi * x)\n",
    "initial_condition_2 = (x) -> sin(5pi * x)\n",
    "initial_condition_3 = (x) -> (x > 1/5 && x < 2/5) ? sin(5pi * x) : 0.0\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8c498d7c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Test finite difference approximation of the wave equation\n",
    "L = 2pi\n",
    "N = 100\n",
    "dx = L/N\n",
    "x = 0:dx:L\n",
    "psi = initial_condition_0.(x)\n",
    "dpsi_dt = zeros(N)\n",
    "c = 1\n",
    "d2psi_dt2 = wave_equation_avx(psi, c, L, N)\n",
    "# Plot initial condition and second time derivative\n",
    "plot(x, psi, label=\"Initial Condition\", title=\"Wave Equation Test\")\n",
    "plot!(x[2:end-1], d2psi_dt2, label=\"Second Time Derivative\", xlabel=\"x\", ylabel=\"Value\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "3bafeb7f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function propagate_psi(psi_0_f::Function; L=1, N=100, c=1, t_0=0, t_f=1, dt=0.01)\n",
    "    # Initial condition at t=0\n",
    "    psi_x_i = psi_0_f.(range(0, L, length=N))\n",
    "\n",
    "    # The string is at rest at t=0\n",
    "    # Array is two elements shorter due to second derivative calculation\n",
    "    dpsi_dt_i = zeros(N-2)\n",
    "\n",
    "    # Boundaries are fixed to zero\n",
    "    psi_x_i[1] = 0\n",
    "    psi_x_i[end] = 0\n",
    "    \n",
    "    # Initialize the array to store the results\n",
    "    n_steps = Int((t_f - t_0) / dt)\n",
    "    psi_x_t = zeros(N, n_steps)\n",
    "    psi_x_t[:, 1] = psi_x_i  \n",
    "\n",
    "    # Time propagation using finite difference method\n",
    "    for n in 1:n_steps-1\n",
    "        d2psi_dt2 = wave_equation_avx(psi_x_t[:, n], c, L, N)\n",
    "        # Use Euler's method to update psi and its time derivative\n",
    "        dpsi_dt_i += d2psi_dt2 * dt\n",
    "        psi_x_t[2:end-1, n+1] = psi_x_t[2:end-1, n] + dpsi_dt_i * dt\n",
    "    end\n",
    "\n",
    "    return psi_x_t\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a44e8f29",
   "metadata": {},
   "outputs": [],
   "source": [
    "t_0 = 0\n",
    "t_f = 1\n",
    "dt = 0.001\n",
    "c=1\n",
    "L=1\n",
    "N=1000"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8dc8f525",
   "metadata": {},
   "source": [
    "## Initial conditions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "43d06d65",
   "metadata": {},
   "outputs": [],
   "source": [
    "solution_1_euler = propagate_psi(initial_condition_1, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)\n",
    "solution_2_euler = propagate_psi(initial_condition_2, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)\n",
    "solution_3_euler = propagate_psi(initial_condition_3, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6aac55ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "function gif_slow(t_f, t_0, dt, L, N, solution_1, solution_2, solution_3)\n",
    "\n",
    "    # Animate the solution and save frames\n",
    "    default(legend=false)\n",
    "    i_total = Int((t_f - t_0) / dt)\n",
    "    x = range(0, L, length=N)\n",
    "\n",
    "    @gif for i in 1:i_total\n",
    "        # create a plot with 3 subplots and a custom layout\n",
    "        p = plot(x, solution_1[:, i], ylim=(-1, 1), title=\"Time: $(round(i*dt, digits=2)) s\", xlabel=\"Position along string\", ylabel=\"Displacement\")\n",
    "        plot!(x, solution_2[:, i])\n",
    "        plot!(x, solution_3[:, i])\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fb5c3687",
   "metadata": {},
   "outputs": [],
   "source": [
    "function distributed_gif(t_f, t_0, dt, L, N, solution_1, solution_2, solution_3, gifname; fps=30, do_palette=false, width=600)\n",
    "    # Create a temporary directory to store frames\n",
    "    tmp_dirname = \"tmp_gif\" * string(rand(1:10000))  # Generate a unique temporary directory name\n",
    "    mkdir(tmp_dirname)\n",
    "\n",
    "    # Animate the solution and save frames\n",
    "    default(legend=false)\n",
    "    i_total = Int((t_f - t_0) / dt)\n",
    "    x = range(0, L, length=N)\n",
    "\n",
    "    @sync @distributed for i in 1:i_total\n",
    "        # create a plot with 3 subplots and a custom layout\n",
    "        p = plot(x, solution_1[:, i], ylim=(-1, 1), title=\"Time: $(round(i*dt, digits=2)) s\", xlabel=\"Position along string\", ylabel=\"Displacement\")\n",
    "        plot!(x, solution_2[:, i])\n",
    "        plot!(x, solution_3[:, i])\n",
    "        savefig(p, \"$(tmp_dirname)/frame_$(lpad(i, 4, '0')).png\")\n",
    "    end\n",
    "    \n",
    "    # Build full path pattern for ffmpeg\n",
    "    frame_pattern = joinpath(tmp_dirname, \"frame_%04d.png\")\n",
    "    \n",
    "    # Use FFMPEG to create GIF\n",
    "    if do_palette\n",
    "        palette_path = joinpath(tmp_dirname, \"palette.png\")\n",
    "        run(`$(FFMPEG.ffmpeg) -framerate $fps \n",
    "            -i $frame_pattern\n",
    "            -vf \"fps=$fps,scale=$width:-1:flags=lanczos,palettegen\"\n",
    "            -y $palette_path`)\n",
    "            \n",
    "\n",
    "        run(`$(FFMPEG.ffmpeg) -framerate $fps \n",
    "            -i $frame_pattern\n",
    "            -i $palette_path\n",
    "            -y $gifname\n",
    "            -hwaccel videotoolbox\n",
    "            -vf \"fps=$fps,scale=$width:-1:flags=fast_bilinear\" \n",
    "            -filter_complex \"fps=$fps,scale=$width:-1:flags=lanczos[x];[x][1:v]paletteuse\" \n",
    "            -gifflags -transdiff\n",
    "            `)\n",
    "    else\n",
    "        run(`$(FFMPEG.ffmpeg) \n",
    "            -hwaccel videotoolbox          \n",
    "            -i $frame_pattern\n",
    "            -vf \"fps=$fps,scale=$width:-1:flags=fast_bilinear\" \n",
    "            -gifflags -transdiff           \n",
    "            -y $gifname`)\n",
    "    end\n",
    "\n",
    "    # Clean up temporary files\n",
    "    rm(tmp_dirname; recursive=true)\n",
    "\n",
    "    display(\"text/html\", \"<img src='$(gifname)'>\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "df49759b",
   "metadata": {},
   "outputs": [],
   "source": [
    "gif_slow(t_f, t_0, dt, L, N, solution_1_euler, solution_2_euler, solution_3_euler)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dde9b844",
   "metadata": {},
   "outputs": [],
   "source": [
    "distributed_gif(t_f, t_0, dt, L, N, solution_1_euler, solution_2_euler, solution_3_euler, \"ex_1_anim_euler.gif\"; do_palette=false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "16d9a78b",
   "metadata": {},
   "outputs": [],
   "source": [
    "function propagate_psi_leapfrog(psi_0_f::Function; L=1, N=100, c=1, t_0=0, t_f=1, dt=0.01)\n",
    "    # Initial condition at t=0\n",
    "    x_i = psi_0_f.(range(0, L, length=N))\n",
    "\n",
    "    # The string is at rest at t=0\n",
    "    # Array is two elements shorter due to second derivative calculation\n",
    "    v_i_slice = zeros(N - 2)\n",
    "\n",
    "    # Boundaries are fixed to zero\n",
    "    x_i[1] = 0\n",
    "    x_i[end] = 0\n",
    "\n",
    "    # Initialize the array to store the results\n",
    "    psi_x_t = zeros(N, Int((t_f - t_0) / dt))\n",
    "    psi_x_t[:, 1] = x_i\n",
    "\n",
    "    # Time propagation using finite difference method\n",
    "    for n in 1:size(psi_x_t, 2)-1\n",
    "        # Calculate acceleration (second time derivative) at current time step\n",
    "        a_i = wave_equation_avx(x_i, c, L, N)\n",
    "\n",
    "        # Update position using Verlet integration (leapfrog)\n",
    "        x_i_next_slice = x_i[2:end-1] + v_i_slice * dt + 0.5 * a_i * dt^2\n",
    "\n",
    "        # Store the next position in the results array\n",
    "        psi_x_t[2:end-1, n+1] = x_i_next_slice\n",
    "\n",
    "        # Calculate acceleration at the next time step for velocity update\n",
    "        x_i_next = psi_x_t[:, n+1]\n",
    "        a_i_next = wave_equation_avx(x_i_next, c, L, N)\n",
    "\n",
    "        # Next time step for velocity\n",
    "        v_i_slice += 0.5 * (a_i + a_i_next) * dt\n",
    "\n",
    "        # Shift current position to next for the next iteration\n",
    "        x_i = x_i_next\n",
    "\n",
    "    end\n",
    "\n",
    "    return psi_x_t\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "eaefc4b6",
   "metadata": {},
   "outputs": [],
   "source": [
    "solution_1_leapfrog = propagate_psi_leapfrog(initial_condition_1, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)\n",
    "solution_2_leapfrog = propagate_psi_leapfrog(initial_condition_2, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)\n",
    "solution_3_leapfrog = propagate_psi_leapfrog(initial_condition_3, L=L, N=N, c=c, t_0=t_0, t_f=t_f, dt=dt)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ea0876af",
   "metadata": {},
   "outputs": [],
   "source": [
    "distributed_gif(t_f, t_0, dt, L, N, solution_1_leapfrog, solution_2_leapfrog, solution_3_leapfrog, \"ex_1_anim_leapfrog.gif\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4a2567b5",
   "metadata": {},
   "source": [
    "We see visibly smaller 'zig-zag' behaviour in displacement between the peaks of y3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "71a26f87",
   "metadata": {},
   "source": [
    "# Problem 1.2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a145daeb",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia (auto threads) 1.11",
   "language": "julia",
   "name": "julia-_auto-threads_-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
